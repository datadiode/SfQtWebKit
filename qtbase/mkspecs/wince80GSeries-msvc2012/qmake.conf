# qmake configuration for Windows Embedded Compact 2013 with VS2012 on X86 targets
#
# This is just a template for creating WEC2013 mkspecs for X86 targets
# Replace the SDK name with actual SDK name.

include(../common/wince/qmake.conf)

CE_SDK                  = Compact2013_SDK_GSeries # replace with actual SDK name
CE_ARCH                 = X86
QT_CONFIG              -= accessibility

DEFINES                += QT_NO_ACCESSIBILITY QT_NO_NATIVE_GESTURES QT_NOSTANDARDSHELL_UI_MODEL SQLITE_MSVC_LOCALTIME_API _CRT_SECURE_NO_DEPRECATE ENABLE_JIT=1 _WIN32_WCE=0x800 $$CE_ARCH _X86_ UNDER_CE WINCE X86

QMAKE_CFLAGS           += -D__STDC__ -Gy -bigobj
QMAKE_CXXFLAGS         += -Gy -bigobj
QMAKE_CFLAGS_RELEASE   -= -O2 -MD
QMAKE_CFLAGS_RELEASE   += -Ot -MT
QMAKE_CFLAGS_RELEASE_WITH_DEBUGINFO -= -O2 -MD
QMAKE_CFLAGS_RELEASE_WITH_DEBUGINFO += -Ot -MT -Zi
QMAKE_CFLAGS_DEBUG     += -Zi -MTd
QMAKE_CFLAGS_DEBUG     -= -MDd
QMAKE_CXXFLAGS_RELEASE -= -O2 -MD
QMAKE_CXXFLAGS_RELEASE += -Ot -MT
QMAKE_CXXFLAGS_RELEASE_WITH_DEBUGINFO -= -O2 -MD
QMAKE_CXXFLAGS_RELEASE_WITH_DEBUGINFO += -Ot -MT
QMAKE_CXXFLAGS_DEBUG   -= -MDd
QMAKE_CXXFLAGS_DEBUG   += -MTd
QMAKE_INCDIR_OPENGL_ES2 += "$$(OPENGL_ES2)/inc"
QMAKE_LFLAGS_CONSOLE    = /SUBSYSTEM:WINDOWSCE,8.00 /MACHINE:X86 #/ENTRY:mainCRTStartup
QMAKE_LFLAGS_WINDOWS    = /SUBSYSTEM:WINDOWSCE,8.00 /MACHINE:X86
QMAKE_LFLAGS_DLL        = /SUBSYSTEM:WINDOWSCE,8.00 /MACHINE:X86 /DLL /SAFESEH:NO
QMAKE_LIBFLAGS_RELEASE  = #/LTCG
QMAKE_LIBS              = coredll.lib
QMAKE_LIBS_CORE         = ole32.lib oleaut32.lib uuid.lib commctrl.lib coredll.lib winsock.lib
QMAKE_LIBS_GUI          = ole32.lib $$QMAKE_LIBS_CORE
QMAKE_LIBS_NETWORK      = ws2.lib $$QMAKE_LIBS_GUI
QMAKE_LIBS_OPENGL       =
QMAKE_LIBS_COMPAT       =
QMAKE_LIBS_OPENVG       =
QMAKE_LIBS_OPENGL_ES2   = libEGL.lib libGLESv2.lib
QMAKE_LIBDIR_OPENGL_ES2 = "$$(OPENGL_ES2)/lib/$$CE_ARCH/retail"
QMAKE_INCDIR_EGL        = "$$(OPENGL_ES2)/inc"
QMAKE_LIBDIR_EGL        = "$$(OPENGL_ES2)/lib/$$CE_ARCH/retail"
QMAKE_INCDIR           += "$$(ICU_DIST)/include"
QMAKE_LIBDIR           += $$(ICU_DIST)/lib/Compact2013_SDK_86Duino_80B

QMAKE_RC                = rc

QMAKE_COMPILER_DEFINES -= _MSC_VER=1400
QMAKE_COMPILER_DEFINES += _MSC_VER=1700
